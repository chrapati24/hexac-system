// TASKMANAGER TESTS PROGRAM - PLEASE TO REDESIGN THIS 
// By SPinti Software(tm)
// Created 08-JUIL-2021
// Updated 12-JUIL-2021

declare/ display_taskmgr()          : level(5)
declare/ kill_selected_process()    : level(3)

// USER
CCP/ /SET.LEVEL = 3

// ---- LISTBOX ----


set/ DisplayMode = 0

function/ display_taskmgr()
        // Create this in new process
        ccp/ /set.level = 2

        // Create new empty process
        @#_pid_taskmgr_ sys/ /process taskmgr.cpc

        // Execute this function in this new process
        CMD/ /pid:%_pid_taskmgr_% /thread /F:__display_taskmgr()

end/ function

function/ __display_taskmgr()
        // ===== Build window =====
        Window/ taskmgr_win

                .title          = "CPinti Core Task manager"
                .parameters     = "TYPE:0 SIZ:1 SIZBTN:1"
                .px             = "140"
                .py             = "100"
                .sx             = "450"
                .sy             = "250"
                .opacity        = "200"
                .WindowColor    = "100,100,100"
                .TitleColor     = "200,200,200"
                .backcolor      = "100,100,100"
                .icon           = "OS/MEDIA/GUI/taskm.png"
                .events         = "%_exe_path_%"

                @#window_handle create/
        End/ Window

        // Getting process list in Listbox format 
        @#list_process_listbox sys/ /process /listb

        // Getting icon process list in Listbox format
        @#list_icon_process_listbox sys/ /process /listb /icon

        // ===== Build listbox =====
        listbox/ listbox_gui
                .handle         = "%window_handle%"
                .parameters     = "EXPLOREMODE:%DisplayMode% CTX:2"
                .px             = "1"
                .py             = "1"
                .sx             = "450"
                .sy             = "250"
                .opacity        = "255"
                .textcolor      = "028,028,028"
                .backcolor      = "255,255,255"
                .text           = "%list_process_listbox%"
                .Image          = "%list_icon_process_listbox%"
                .events         = "%_exe_path_%"
                create/
        End/ listbox

        ccp/ /set.level = 3

        // Recuperer la liste des nom de processus dans un tableau CpcdosC+
        sys/ /process /list /index /array list_process_pid

        // Config contextmenu
        set/ listbox_gui.ctx_text[0] = Arreter processus
        set/ listbox_gui.ctx_action[0] = /F:kill_selected_process() \#NO-FN

        set/ listbox_gui.ctx_text[1] = Suspendre processus
        set/ listbox_gui.ctx_action[1] =  

        set/ listbox_gui.ctx_text[2] = Reprendre processus
        set/ listbox_gui.ctx_action[2] =  

        set/ listbox_gui.ctx_text[3] = Annuler
        set/ listbox_gui.ctx_action[3] =  


        set/ listbox_gui.ctx_number = 4

end/ function


// ===== GUI events =====

function/ taskmgr_win.WindowClosed()
        // Close process (variable _pid_taskmgr_ generated by kernel)
        ccp/ /set.level = 3
        close/ /pid:%_pid_taskmgr_%
end/ function

Function/ listbox_gui.selected(item_str,item_index)
        ccp/ /set.level = 1

end/ Function


Function/ kill_selected_process()
        ccp/ /set.level = 1


        // Getting item name
        @#item_index listbox/ /selected_index listbox_gui

        // Getting PID number from array
        close/ /PID %list_process_pid[item_index]%

        ccp/ /pause 100

        // Refresh list
        /F:refresh()

End/ function

Function/ refresh()
        // This function refresh list

        ccp/ /set.level = 1

        // Getting process list in Listbox format 
        @#list_process_listbox sys/ /process /listb

        // Getting icon process list in Listbox format
        @#list_icon_process_listbox sys/ /process /listb /icon
        
        listbox/ /modif:listbox_gui
                .text           = "%list_process_listbox%"
                .Image          = "%list_icon_process_listbox%"
                create/
        End/ listbox

        ccp/ /set.level = 3
        sys/ /process /list /index /array list_process_pid

End/ function

Function/ listbox_window.Resize(sizeup)
	// If user change window size

	// Getting window size informations
	window/ /modif:listbox_window
		@#Win_Size_X .sx
		@#Win_Size_Y .sy
	end/ window
	
        // According textbox size
        set/ Win_Size_Y = /C(%Win_Size_Y% - 23)

        // And modify listbox object size
	listbox/ /modif:listbox_gui
		.sx     = "%Win_Size_X%"
		.sy     = "%Win_Size_Y%"
		create/
	end/ listbox

End/ function














